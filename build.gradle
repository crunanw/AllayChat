plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta13'
}

group = 'net.voxelarc'
version = '1.0.0-SNAPSHOT'

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.gradleup.shadow'

    group = 'net.voxelarc'
    version = '1.0.0-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = 'papermc'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven { url 'https://repo.triumphteam.dev/snapshots/' }
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    }

    dependencies {
        compileOnly('org.projectlombok:lombok:1.18.30')
        annotationProcessor 'org.projectlombok:lombok:1.18.30'

        compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
        compileOnly 'me.clip:placeholderapi:2.11.6'
    }

    build {
        dependsOn shadowJar
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':API')

    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'com.zaxxer:HikariCP:6.3.0'

    implementation ("dev.triumphteam:triumph-cmd-bukkit:2.0.0-SNAPSHOT") { exclude group: "com.google.code.gson" }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    dependsOn(jar)

    archiveFileName = "${project.name} v${archiveVersion.get()}.jar"

    relocate 'xyz.xenondevs', 'net.voxelarc.allaychat.libraries.gui'
    relocate 'dev.triumphteam.cmd', 'net.voxelarc.allaychat.libraries.cmd'
    relocate 'de.tr7zw.changeme.nbtapi', 'net.voxelarc.allaychat.libraries.nbt'

    from sourceSets.getByName("main").output
    configurations = [project.configurations.getByName("runtimeClasspath")]

}